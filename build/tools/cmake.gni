template("cmake") {
  # required: cmake_root_dir, cmake_outputs
  # optional: cmake_options, cmake_build_dir, cmake_install_dir
  action("$target_name") {
    cmake_options = ["CMAKE_VERBOSE_MAKEFILE:BOOL=ON"]
    cmake_build_dir = target_out_dir + "/" + target_name
    cmake_install_dir = root_out_dir + "/" + target_name
    forward_variables_from(invoker,
                           "*",
                           [
                             "script",
                             "args",
                             "input",
                             "output_name",
                           ])
     toolset = "default"
    version = "default"
    if (is_win) {
      version = winver
      toolset = "v${vs_version}"
      if (is_winxp) {
        if (vs_version > 141) {
          toolset = "v141_xp"
        } else if (vs_version >= 110) {
          toolset = "v${vs_version}_xp"
        }
      }
    } else {
      toolset = "linux"
      version = "linux"
    }

    script = "//build/tools/cmake.py"
    args = [
      rebase_path(cmake_root_dir, root_build_dir),
    ]

    options = ""
    size = 0
    foreach(option, cmake_options) {
      size += 1
    }

    i = 0
    foreach(option, cmake_options) {
      i += 1
      options += option
      if (i != size) {
        options += ","
      }
    }

    args += [
      options,
      rebase_path(cmake_build_dir, root_build_dir),
      rebase_path(cmake_install_dir, root_build_dir),
      "$target_cpu",
      "$is_debug",
      "$static_link_crt",
      toolset,
      version,
    ]
    inputs = [ cmake_root_dir ]
    outputs = cmake_outputs
  }
}
